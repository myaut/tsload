from pathutil import *

import sys

tgtroot = 'tsload-standalone'
target = 'tsexperiment'

Import('env')

cmd = env.Clone()

def InstallExperiment(experiment_name):
    for file in Dir(experiment_name).glob('*'):
        cmd.Depends(tsexperiment,  file)
        cmd.InstallTarget(tgtroot, PathJoin(cmd['INSTALL_VAR'], experiment_name), file)

ExpErrGenBuilder = Builder(action = Action('%s $TSLOADPATH/tools/genexperr.py $SOURCE > $TARGET' % (sys.executable),
                                           cmd.PrintCommandLine('GENEXPERR')))
cmd.Append(BUILDERS = {'ExpErrGenBuilder': ExpErrGenBuilder})
experr_c = cmd.ExpErrGenBuilder('experr.c', 'include/experiment.h')

cmd.UseSubsystems('tse_error', 'log', 'tsload', 'tsfile', 'hi_obj', 'run')
objects = cmd.CompileProgram()
tsexperiment = cmd.LinkProgram(target, objects)

i_tsexperiment = cmd.InstallTarget(tgtroot, cmd['INSTALL_BIN'], tsexperiment)


InstallExperiment('sample')
