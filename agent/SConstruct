import os
import sys

import shutil

from SCons.Action import CommandAction, ActionFactory
from SCons.Errors import StopError 
from SCons.Script.SConscript import SConsEnvironment

from optparse import SUPPRESS_HELP 

sys.path.append(os.path.join('tools', 'build'))

from pathutil import *
import tstest

# ------------------------
# ENVIRONMENT HELPERS

def AddEnableOption(option, dest, default, help):
    (enable_help, disable_help) = (SUPPRESS_HELP, 'Disable ' + help + ' (enabled by default)')         \
                                    if default                                                         \
                                    else ('Enable ' + help + ' (disabled by default)', SUPPRESS_HELP)
    
    AddOption('--enable-'+option,  dest=dest, action="store_true", default=default,
              help=enable_help)
    AddOption('--disable-'+option,  dest=dest, action="store_false", default=default, 
              help=disable_help)


SConsEnvironment.Chmod = ActionFactory(os.chmod,
        lambda dest, mode: 'Chmod("%s", 0%o)' % (dest, mode))

# ------------------------
# BUILD OPTIONS

AddOption('--verbose-build',  dest='verbose_build', action="store", default='',
          metavar='LIST', 
          help='Allow additional output: comma-separated list of cmdline, plat, tsdoc, etrace')

AddOption('--mach',  dest='mach', action="store", default='',
          help='Machine architecture')

# buildid file
AddOption('--update-build',  dest='update_build', action="store_true",
          help='Update build file')

AddOption('--update-doc',  dest='update_doc', action="store_true",
          help='Update documentation')
AddOption('--doc-format',  dest='doc_format', action="store", default='html',
          metavar='FORMAT', help='Documentation format (markdown or html)')


AddEnableOption('mempool-alloc',  dest='mempool_alloc', default=False,
          help='mempool allocator [EXPERIMENTAL]')
AddEnableOption('mempool-valgrind',  dest='mempool_valgrind', default=False,
          help='adds Valgrind support to mempool allocator. If valgrind development package is not installed, this option is useless')

# Debug options
AddEnableOption('debug', dest='debug', default=True, 
                help='debug build')
AddEnableOption('trace', dest='trace', default=False, 
                help='various tracing facilities')

# Dynamic event tracing framework support
AddEnableOption('usdt', dest='usdt', default=True, 
                help='User Defined Tracing (SystemTap, DTrace)')
AddEnableOption('etw', dest='etw', default=True, 
                help='Event Tracing for Windows')

# tsloadd config options
AddOption('--tsload-log-file', dest='tsload_log_file', action='store', default='tsload.log',
          metavar='LOGFILE', help='Log file for tsload daemon (specified in tsloadd configuration)')
AddOption('--tsload-server', dest='tsload_server', action='store', default='localhost:9090',
          metavar='SERVER:PORT', help='Default server for tsload agent (specified in tsloadd configuration).')

AddOption('--with-windows-sdk', dest='with_windows_sdk', action='store', default='',
          metavar='WINSDK', help='Path to windows SDK')

# test options
AddOption('--runner-threads',  dest='runner_threads', action="store", default='1',
          metavar='COUNT', help='Number of threads in test suite runner')
AddOption('--test-dir',  dest='test_directory', action="store", default='',
          metavar='TESTDIR', help='Path to test directory')

# tsload options
AddEnableOption('fast-randgen',  dest='fast_randgen', default=False,
                help='Use faster (but less precise) method in rg_calculate_double()')

# installation option
AddEnableOption('zip-packages', dest='zip_packages', default=False, 
                help='create individial zip packages like tsload-doc-*.zip')

# hostinfo options
if sys.platform == 'linux2':
    AddEnableOption('lvm2', dest='lvm2', default=True,
                    help='support for LVM2 objects to HostInfo')
    
zfs = sys.platform == 'sunos5'
zfs_optional = sys.platform == 'linux2'
if zfs or zfs_optional:
    AddEnableOption('zfs', dest='zfs', default=zfs,
                    help='support for ZFS objects to HostInfo')

# Installation directories options are specified below

# -------------------------
# PREPARE BUILD ENVIRONMENT

env = DefaultEnvironment(ENV = {'PATH': os.environ['PATH']},
                         TARGET_ARCH = GetOption('mach'))

# name of the project, version, root path
env['TSLOADPATH'] = Dir('#').abspath
env['TSPROJECT'] = 'tsload'
env['TSVERSION'] = '0.2.a1'
env['TSNAME'] =  '-'.join((env['TSPROJECT'], env['TSVERSION'], sys.platform))

# this variable is a hint for build system that we 
# are building TSLoad itself, not some extensions 
env['_INTERNAL'] = True

env['DEBUG'] = GetOption('debug')
env['VERBOSE_BUILD']= GetOption('verbose_build').split(',')

env['TARGET_ARCH'] = GetOption('mach')

SConscript('SConscript.env.py', 'env')

# --------------------------
# PLATFORM-SPECIFIC BUILD
SConscript('SConscript.plat.py', 'env')

if sys.platform == 'win32':
    env.Macroses('_WINSOCKAPI_')
    env.Macroses('_CRT_SECURE_NO_WARNINGS')

# --------------------------
# ETRACE PREPROCESSOR
env['ETRACEENABLED'] = GetOption('usdt') or GetOption('etw')
SConscript('SConscript.etrace.py', 'env')
    
# ----------------------------
# CONFIGURE

env.Append(CPPPATH = [env.BuildDir('include')])

# XXX: conf.CheckLib poisons LIBS, purify it!
env['_LIBS'] = env['LIBS']
SConscript('SConscript.conf.py', 'env')
env['LIBS'] = env['_LIBS']

# ------------------------
# INSTALLATION DIRECTORIES

SConscript('SConscript.install.py', 'env')

env['LOAD_LOGFILE'] = GetOption('tsload_log_file')
if not PathIsAbs(env['LOAD_LOGFILE']):
    env['LOAD_LOGFILE'] = PathJoin(env['PREFIX'], env['INSTALL_VAR'],  env['LOAD_LOGFILE'])

env['LOAD_MODPATH'] = PathJoin(env['PREFIX'], env['INSTALL_MOD_LOAD'])

# Specify RPATH to shared libraries
if env.SupportedPlatform('posix') and env['CC'] == 'gcc':
    env.Append(LINKFLAGS = ['-zorigin'])
    
    lib_rel_path = PathRelative(env['INSTALL_LIB'], env['INSTALL_BIN'])
    env.Append(RPATH = env.Literal(os.path.join('\\$$ORIGIN', lib_rel_path)))

#------------
# DOC
SConscript('SConscript.doc.py', 'env')

#------------
# DEVEL
SConscript('SConscript.devel.py', 'env')

# -------------------------
# BUILD ITEMS   

build_items = [ # tsload-core
               ('lib', 'libtscommon'),
               ('lib', 'libtsjson'),
               ('lib', 'libtsobj'),
               ('lib', 'libhostinfo'),
               ('lib', 'libtsfile'),
               ('lib', 'libtsload'),               
               ('cmd', 'tshostinfo'),
               ('cmd', 'tsfutil'),
               ('cmd', 'tsgenmodsrc'),               
               
               # tsload-standalone
               ('cmd', 'tsexperiment'),
               
               # tsload-agents
               # ('lib', 'libtsagent'),
               # ('cmd', 'tsgenuuid'),               
               # ('cmd', 'tsloadd'),
               
               # ('tools', 'bench/libjson')
               ]

# ------------
# BUILD

for tgtdir, target in build_items:
    path = PathJoin(tgtdir, target)
    variant_dir = env.BuildDir(path)
    
    SConscript(PathJoin(path, 'SConscript'), 'env',
               variant_dir = variant_dir)

# Iterate over modules directories and add modules to build items
for mod_type in ('load', ):
    for mod in Dir('mod').Dir(mod_type).glob('*'):
        mod_path = str(mod)
        variant_dir = env.BuildDir(mod_path)
        
        SConscript(PathJoin(mod_path, 'SConscript'), 'env',
                   variant_dir = variant_dir)

#------------
# INSTALL various files

# ------------
# TESTS
if 'tests' in BUILD_TARGETS and 'systests' in BUILD_TARGETS:
    raise StopError('Unit and system testing is mutually exclusibe')

if 'tests' in BUILD_TARGETS:
    env['TEST_MODE_INT'] = False
    env['TEST_SUITE'] = File(PathJoin('test', 'test.list'))
        
    SConscript(PathJoin('test', 'SConscript'), 'env',
               variant_dir = PathJoin('#build', 'test'))
elif 'systests' in BUILD_TARGETS:
    env['TEST_MODE_INT'] = True
    env['TEST_SUITE'] = File(PathJoin('test', 'systest.list'))
        
    SConscript(PathJoin('test', 'SConscript'), 'env',
               variant_dir = PathJoin('#build', 'test', 'tsexperiment'))
    
#------------
# DOC (again)
if 'doc' in BUILD_TARGETS:
    SConscript(PathJoin('doc', 'SConscript'), 'env',
               variant_dir = PathJoin('#build', 'doc'))

#------------
# ZIP

packages = ['tsload-core', 
            'tsload-agents', 
            'tsload-standalone', 
            'tsload-doc',
            'tsload-modules',
            'tsload-devel']

# Packages dependencies

env.Depends('tsload-agents', 'tsload-core')
env.Depends('tsload-standalone', 'tsload-core')

all_files = []

for pkg in packages:
    aliases = env.Alias(pkg)
    alias = aliases[0]
    
    zip_name = '-'.join((pkg, env['TSVERSION']))   + '.zip'
    zip_path = PathJoin('#build', 'pkg', zip_name)
    
    if GetOption('zip_packages'):
        zip_files = env.ZipArchive(zip_path, alias.children())
        env.Depends('zip', pkg)
        env.Alias('zip', zip_files)
    
    all_files.extend(alias.children())
    
zip_name = '-'.join((env['TSPROJECT'], env['TSVERSION'], env['TARGET_ARCH']))   + '.zip'
zip_path = PathJoin('#build', 'pkg', zip_name)
zip_file = env.ZipArchive(zip_path, all_files)
env.Alias('zip', zip_file)

# By default only build files, not install/build docs/make packages
Default('build/build')